#!/usr/bin/awk -f

BEGIN {
    FS = ", "
    dx[1] = 0; dx[2] = 1; dx[3] =  0; dx[4] = -1
    dy[1] = 1; dy[2] = 0; dy[3] = -1; dy[4] =  0
    dir_index = 1
    x = 0
    y = 0
}

{
    for (i = 1; i <= NF; ++i) {
        dir = substr($i, 1, 1)
        dist = substr($i, 2) + 0

        if (dir == "R")
            ++dir_index
        else
            --dir_index

        if (dir_index == 0)
            dir_index = 4
        else if (dir_index == 5)
            dir_index = 1


        for (j = 1; j <= dist; ++j) {
            x += dx[dir_index]
            y += dy[dir_index]
            if (visited[x, y])
                next
            ++visited[x, y]
        }
    }
}

END {
    x = x < 0 ? -x : x
    y = y < 0 ? -y : y
    print x + y
}
