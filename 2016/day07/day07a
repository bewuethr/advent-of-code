#!/usr/bin/awk -f

function has_abba(str,    i) {
    for (i = 1; i <= length(str) - 3; ++i) {
        if ( \
            substr(str, i+1, 1) == substr(str, i+2, 1) && \
            substr(str, i, 1) == substr(str, i+3, 1) && \
            substr(str, i, 1) != substr(str, i+1, 1) \
        ) {
            return 1
        }
    }
    return 0
}

BEGIN { FS = "[][]" }

{
    for (i = 2; i <= NF - 1; i += 2) {
        if (has_abba($i))
            next
    }
    for (i = 1; i <= NF; ++i) {
        if (has_abba($i)) {
            ++tls
            next
        }
    }
}

END { print tls }
