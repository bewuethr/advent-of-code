#!/usr/bin/awk -f

{
    instr[NR] = $0
    regs["c"] = 1
}

END {
    i = 1
    while (1) {
        if (!(i in instr))
            break
        split(instr[i], arr)

        if (instr[i] ~ /cpy/) {
            regs[arr[3]] = arr[2] ~ /[abcd]/ ? regs[arr[2]] : arr[2]
            ++i
        }

        else if (instr[i] ~ /inc/) {
            ++regs[arr[2]]
            ++i
        }

        else if (instr[i] ~ /dec/) {
            --regs[arr[2]]
            ++i
        }

        else if (instr[i] ~ /jnz/) {
            if ((arr[2] ~ /[abcd]/ && regs[arr[2]] != 0) || \
                (arr[2] ~ /[0-9]/ && arr[2] != 0))
            {
                i += arr[3]
            }
            else
                ++i
        }
    }
    print regs["a"]
}
