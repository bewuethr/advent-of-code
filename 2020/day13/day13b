#!/usr/bin/awk -Mf

function extended_euclid(a, b,   b0, t, q, x0, x1) {
	b0 = b
	x0 = 0
	x1 = 1

	if (b == 1) {
		return 1
	}

	while (a > 1) {
		q = int(a / b)
		t = b
		b = a % b
		a = t
		t = x0
		x0 = x1 - q * x0
		x1 = t
	}

	return x1 < 0 ? x1 + b0 : x1
}

BEGIN {	FS = "," }

NR == 2 {
	N = 1
	for (i = 1; i <= NF; ++i) {
		if ($i != "x") {
			n[i] = $i
			a[i] = ($i - i + 1) % $i
			N *= $i
		}
	}
}

END {
	for (i in n) {
		p = int(N / n[i])
		result += a[i] * extended_euclid(p, n[i]) * p
	}

	print result % N
}
